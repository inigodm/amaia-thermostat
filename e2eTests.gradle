sourceSets {
    e2eTests {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/e2eTests/kotlin')
        }
        resources.srcDir file('src/e2eTests/resources')
    }
}
rootProject.tasks.named("processE2eTestsResources") {
    duplicatesStrategy = 'include'
}

task e2eTests(type: Test) {
    useJUnitPlatform()
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\n\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
    testClassesDirs = sourceSets.e2eTests.output.classesDirs
    classpath = sourceSets.e2eTests.runtimeClasspath
}

dependencies {
    e2eTestsImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.50"
    e2eTestsImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    e2eTestsRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    e2eTestsImplementation 'org.springframework.boot:spring-boot-starter-test'
    e2eTestsImplementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    e2eTestsImplementation "org.assertj:assertj-core:${assertJVersion}"
    e2eTestsImplementation "org.mockito:mockito-core:${mockitoVersion}"
    e2eTestsImplementation files('lib/E2EHelper-0.0.3-SNAPSHOT.jar')
    e2eTestsImplementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.9.3'
    e2eTestsImplementation "org.xerial:sqlite-jdbc:${sqliteVersion}"
    e2eTestsImplementation group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.8.2'

}
